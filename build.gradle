plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

repositories {
    mavenCentral()
    // spigot
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    // paper
    maven { url = 'https://papermc.io/repo/repository/maven-public/' }
    // languages
    maven { url = 'https://repo.minebench.de/' }
    // commands
    maven { url = 'https://repo.aikar.co/content/groups/aikar/' }
    // helper
    maven { url = 'https://repo.lucko.me' }
    // towny
    maven { url 'https://jitpack.io' }
    // nyaacore
    maven { url 'https://ci.nyaacat.com/maven/' }
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'

    compileOnly 'io.papermc.paper:paper-api:1.17.1-R0.1-SNAPSHOT'

    compileOnly('me.lucko:helper:5.6.8')
    compileOnly('me.lucko:helper-sql:1.3.2')
    compileOnly('me.lucko:helper-profiles:1.2.0')

    implementation('org.spongepowered:configurate-core:4.1.1')
    implementation('org.spongepowered:configurate-yaml:4.1.1')
    implementation('de.themoep.utils:lang-bukkit:1.3-SNAPSHOT')
    implementation('co.aikar:acf-paper:0.5.0-SNAPSHOT')

    // luckperms
    compileOnly('net.luckperms:api:5.3')

    // towny (for linking to system account)
    compileOnly('com.github.TownyAdvanced:Towny:0.97.0.11')
    // nyaacore (for implementing system balance)
    compileOnly('cat.nyaa:nyaacore:8.1-SNAPSHOT')

    compileOnly('net.milkbowl.vault:VaultAPI:1.7') { transitive = false }
    compileOnly('net.ess3:EssentialsX:2.18.2') { transitive = false }
}

test {
    useJUnitPlatform()
}

shadowJar {
    archiveClassifier.set("")
}

ext.pluginNameUpper = "MoeCore"
ext.majorVersion = 1
ext.minorVersion = 0
ext.minecraftVersion = "1.17.1"
java.sourceCompatibility = JavaVersion.VERSION_16
java.targetCompatibility = JavaVersion.VERSION_16

group = 'co.mcsky'
archivesBaseName = "${pluginNameUpper}-mc$minecraftVersion"
version = "$majorVersion.$minorVersion".toString()

// source file modification (modify version string)
processResources {
    filesMatching("**/plugin.yml") {
        expand 'version': project.version
    }
}

compileJava {
    options.compilerArgs += ["-parameters", "-Xlint:deprecation"]
    options.encoding = 'UTF-8'
}
